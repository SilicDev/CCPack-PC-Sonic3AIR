
//# script-feature-level(2)

constant u8 CCStatusCode.SUCCESS		= 0x00		// The effect executed successfully
constant u8 CCStatusCode.FAILURE		= 0x01		// The effect failed to trigger, but is still available for use; viewer(s) will be refunded
constant u8 CCStatusCode.UNAVAILABLE	= 0x02		// Same as FAILURE but the effect is no longer available for use for the remainder of the game
constant u8 CCStatusCode.RETRY			= 0x03		// The effect cannot be triggered right now, try again in a few seconds - note: this is the "normal" failure response
constant u8 CCStatusCode.PAUSED			= 0x06		// The timed effect has been paused and is now waiting
constant u8 CCStatusCode.RESUME			= 0x07		// The timed effect has been resumed and is counting down again
constant u8 CCStatusCode.FINISHED		= 0x08		// The timed effect has finished

constant u8 object_size = 0x4A

global bool timerA

function void PreFrameUpdate()
{
    if (global.characters == CHARS_TAILS_ALONE)
    {
        tails.control_counter = 600 // don't let the AI mess with us
    }
}

function u8 Game.triggerCrowdControlEffect(string effectCode)
{
    return Game.triggerCrowdControlEffect(effectCode, 1)
}

function u8 Game.triggerCrowdControlEffect(string effectCode, u16 quantity)
{
	// Debug output
	//debugLog(effectCode)

	if (effectCode == "AddRing")
	{
		addRings(quantity)
		return CCStatusCode.SUCCESS
	}
	if (effectCode == "TakeRing")
    {
        if (player.total_rings < quantity)
        {
            return CCStatusCode.FAILURE
        }
        D0 = -quantity
        AddRings()
		return CCStatusCode.SUCCESS
    }
    if (effectCode == "CharSonic")
    {
		u32 ABackUp = A0
		A0 = 0xffffb000
        CrowdControl.SwapToSonic()
        A0 = ABackUp
		return CCStatusCode.SUCCESS
    }
    if (effectCode == "CharTails")
    {
		u32 ABackUp = A0
		A0 = 0xffffb000
        CrowdControl.SwapToTails()
        A0 = ABackUp
		return CCStatusCode.SUCCESS
    }
    if (effectCode == "CharKnuckles")
    {
		u32 ABackUp = A0
		A0 = 0xffffb000
        CrowdControl.SwapToKnuckles()
        A0 = ABackUp
		return CCStatusCode.SUCCESS
    }
    if (effectCode == "shieldFire")
	{
		u32 ABackUp = A1
		A1 = 0xffffb000
		ItemEffect.ApplyShieldFire()
		A1 = ABackUp
		return CCStatusCode.SUCCESS
	}
	if (effectCode == "shieldBubble")
	{
		u32 ABackUp = A1
		A1 = 0xffffb000
		ItemEffect.ApplyShieldBubble()
		A1 = ABackUp
		return CCStatusCode.SUCCESS
	}
	if (effectCode == "shieldThunder")
	{
		u32 ABackUp = A1
		A1 = 0xffffb000
		ItemEffect.ApplyShieldLightning()
		A1 = ABackUp
		return CCStatusCode.SUCCESS
	}
	if (effectCode == "shieldBlue")
	{
		u32 ABackUp = A1
		A1 = 0xffffb000
		ItemEffect.applyShield(0, addressof(InstaShield.Init), SFX_CLASSICSHIELD)
		A1 = ABackUp
		return CCStatusCode.SUCCESS
	}
    if (effectCode == "Bumper")
    {
        if (allocDynamicObjectStd())
        {
            objA1.update_address = addressof(Bumper.Init)//0x032ef0
            objA1.base_state = 0
            objA1.position.x = camera.position.x
            objA1.position.y = camera.position.y
            objA1.position.x.u16 += System.rand() & 0x3F - 0x20
            objA1.position.y.u16 += System.rand() & 0x3F - 0x20
            objA1.position.x.u16 += (getScreenWidth() / 2)
            objA1.position.y.u16 += (getScreenHeight() / 2)
            if (char.flags & char.flag.FACING_LEFT)
            {
                objA1.position.x.u16 -= 64 + char.box_size.x
            }
            else
            {
                objA1.position.x.u16 += 64
            }
            u16[A1 + 0x2e] = 600
            objA1.countdown_callback = addressof(UnloadObject)
            return CCStatusCode.SUCCESS
        }
		return CCStatusCode.FAILURE
    }
	if (effectCode == "Slap" && u8[0xffffb000 + 0x2e] == 0)
	{
		timerA = true
		u32 ABackUp = A0
		A0 = 0xffffb000
		// u8[0xffffb000 + 0x20] = char.state.GOT_HURT
		// u8[0xffffb000 + 0x05] = 0x04
		// // char.base_state = 
		// ItemEffect.applyShield(0, addressof(InstaShield.Init), SFX_CLASSICSHIELD)
		// A1 = ABackUp

		

		// char.base_state = 0x04
		Character.LandingOnGroundNoSpindash()
		char.flags |= (char.flag.IN_AIR | char.flag.CONTROL_LOCK)
		if (char.flags & char.flag.UNDERWATER)
		{
			char.velocity.x = -0x100
			char.velocity.y = -0x200
		}
		else
		{
			char.velocity.x = -0x500
			char.velocity.y = -0x700
		}
		if (char.flags & char.flag.FACING_LEFT)
		{
			char.velocity.x = -char.velocity.x
		}

		char.groundspeed = 0
		char.state = char.state.GOT_HURT_PANIC
		char.invuln.countdown = 0

		if (char.character == CHARACTER_SONIC)
		{
			// Reset drop dash
			sonic.dropdash_counter = 0
		}
		
		A0 = ABackUp
		return CCStatusCode.SUCCESS
	}

	// For unknown effects
	return CCStatusCode.UNAVAILABLE
}

// return is unused for now, just adding in case we have additional fail conditions
function bool CrowdControl.SwapToSonic()
{
    char.update_address = 0x010a94
    u16 sprio = u16[0xFFB00A]
    char.base_state = 0x00
    char.sprite_attributes = 0x0680 | (0x8000 & sprio)
    global.characters = CHARS_SONIC_ALONE
    CrowdControl.SnapTails(3)
    //Character.Initialization.Sonic()
    return true
}

function bool CrowdControl.SwapToTails()
{
    char.update_address = 0x01365c
    u16 sprio = u16[0xFFB00A]
    char.base_state = 0x00
    char.sprite_attributes = 0x06A0 | (0x8000 & sprio)
    global.characters = CHARS_TAILS_ALONE
    CrowdControl.SnapTails(1)
    //Character.Initialization.Tails()
    return true
}

function bool CrowdControl.SwapToKnuckles()
{
    char.update_address = 0x016444
    u16 sprio = u16[0xFFB00A]
    char.base_state = 0x00
    char.sprite_attributes = 0x0680 | (0x8000 & sprio)
    global.characters = CHARS_KNUCKLES_ALONE
    CrowdControl.SnapTails(3)
    //Character.Initialization.Knuckles()
    return true
}

function void CrowdControl.SnapTails(u8 character)
{
    for (u8 i = 95; i < 108; i++)
    {
        if (u16[0xFFB000 + (object_size * i) + 0x02] == 0x609A)
        {
            if (character == 1)
            {
                u16[0xFFB000 + (object_size * i) + 0x30] = 0xB000
            }
            if (character == 2)
            {
                u16[0xFFB000 + (object_size * i) + 0x30] = 0xB04A
            }
            if (character == 3)
            {
                u16[0xFFB000 + (object_size * i) + 0x30] = 0x0000
            }
        }
    }
}

function bool Standalone.onWriteToSpriteTable(s16 px, s16 py, u16 renderQueue)
{

	bool prioFlag = (objA0.sprite_attributes & sprite_attribute.PRIORITY) != 0

    if (objA0.update_address == addressof(Bumper.MainUpdate) && u16[A0 + 0x2e] != 0)
	{
		return Bumper.onWriteToSpriteTable(px, py, renderQueue, prioFlag)
	}
	return base.Standalone.onWriteToSpriteTable(px, py, renderQueue)
}


// Dev feature just for testing - must get disabled / removed before release!
#if 1

global bool key2_state = false

function void Debugging.debugDraw()
{
	// Call the base implementation (which already uses keys 0 and 1)
	base.Debugging.debugDraw()

	if (Key2)
	{
		if (!key2_state)
		{
			Game.triggerCrowdControlEffect("Bumper", 1)
			key2_state = true
		}
	}
	else
	{
		key2_state = false
	}

}

#endif

include EnemySpawn
include Bumpers