
//# script-feature-level(2)

constant u8 CCStatusCode.SUCCESS		= 0x00		// The effect executed successfully
constant u8 CCStatusCode.FAILURE		= 0x01		// The effect failed to trigger, but is still available for use; viewer(s) will be refunded
constant u8 CCStatusCode.UNAVAILABLE	= 0x02		// Same as FAILURE but the effect is no longer available for use for the remainder of the game
constant u8 CCStatusCode.RETRY			= 0x03		// The effect cannot be triggered right now, try again in a few seconds - note: this is the "normal" failure response
constant u8 CCStatusCode.PAUSED			= 0x06		// The timed effect has been paused and is now waiting
constant u8 CCStatusCode.RESUME			= 0x07		// The timed effect has been resumed and is counting down again
constant u8 CCStatusCode.FINISHED		= 0x08		// The timed effect has finished

constant u8 object_size = 0x4A

function void PreFrameUpdate()
{
    if (global.characters == CHARS_TAILS_ALONE)
    {
        tails.control_counter = 600 // don't let the AI mess with us
    }
}

function u8 Game.triggerCrowdControlEffect(string effectCode)
{
    return Game.triggerCrowdControlEffect(effectCode, 1)
}

function u8 Game.triggerCrowdControlEffect(string effectCode, u16 quantity)
{
	// Debug output
	//debugLog(effectCode)

	if (effectCode == "AddRing")
	{
		addRings(quantity)
		return CCStatusCode.SUCCESS
	}
	if (effectCode == "TakeRing")
    {
        if (player.total_rings < quantity)
        {
            return CCStatusCode.FAILURE
        }
        D0 = -quantity
        AddRings()
		return CCStatusCode.SUCCESS
    }
    if (effectCode == "CharSonic")
    {
		u32 ABackUp = A0
		A0 = 0xffffb000
        SwapToSonic()
        A0 = ABackUp
		return CCStatusCode.SUCCESS
    }
    if (effectCode == "CharTails")
    {
		u32 ABackUp = A0
		A0 = 0xffffb000
        SwapToTails()
        A0 = ABackUp
		return CCStatusCode.SUCCESS
    }
    if (effectCode == "CharKnuckles")
    {
		u32 ABackUp = A0
		A0 = 0xffffb000
        SwapToKnuckles()
        A0 = ABackUp
		return CCStatusCode.SUCCESS
    }

	// For unknown effects
	return CCStatusCode.UNAVAILABLE
}

// return is unused for now, just adding in case we have additional fail conditions
function bool SwapToSonic()
{
    char.update_address = 0x010a94
    u16 sprio = u16[0xFFB00A]
    char.base_state = 0x00
    char.sprite_attributes = 0x0680 | (0x8000 & sprio)
    global.characters = CHARS_SONIC_ALONE
    SnapTails(3)
    //Character.Initialization.Sonic()
    return true
}

function bool SwapToTails()
{
    char.update_address = 0x01365c
    u16 sprio = u16[0xFFB00A]
    char.base_state = 0x00
    char.sprite_attributes = 0x0680 | (0x8000 & sprio)
    global.characters = CHARS_TAILS_ALONE
    SnapTails(1)
    //Character.Initialization.Tails()
    return true
}

function bool SwapToKnuckles()
{
    char.update_address = 0x016444
    u16 sprio = u16[0xFFB00A]
    char.base_state = 0x00
    char.sprite_attributes = 0x0680 | (0x8000 & sprio)
    global.characters = CHARS_KNUCKLES_ALONE
    SnapTails(3)
    //Character.Initialization.Knuckles()
    return true
}

function void SnapTails(u8 character)
{
    for (u8 i = 95; i < 108; i++)
    {
        if (u16[0xFFB000 + (object_size * i) + 0x02] == 0x609A)
        {
            if (character == 1)
            {
                u16[0xFFB000 + (object_size * i) + 0x30] = 0xB000
            }
            if (character == 2)
            {
                u16[0xFFB000 + (object_size * i) + 0x30] = 0xB04A
            }
            if (character == 3)
            {
                u16[0xFFB000 + (object_size * i) + 0x30] = 0x0000
            }
        }
    }
}


// Dev feature just for testing - must get disabled / removed before release!
#if 1

global bool key2_state = false

function void Debugging.debugDraw()
{
	// Call the base implementation (which already uses keys 0 and 1)
	base.Debugging.debugDraw()

	if (Key2)
	{
		if (!key2_state)
		{
			Game.triggerCrowdControlEffect("AddRing", 1)
			key2_state = true
		}
	}
	else
	{
		key2_state = false
	}

}

#endif


include EnemySpawn
